buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.7'
    }
}

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.vielengames"
        minSdkVersion 10
        targetSdkVersion 21
        versionCode 1002019
        versionName "1.2.19"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ext.betaDistributionReleaseNotesFilePath = "crashlytics_release_notes.txt"
        }
    }
    flavorDimensions 'backend'
    productFlavors {
        production {
            flavorDimension 'backend'
            applicationId "com.vielengames"
            buildConfigField 'String', 'ENDPOINT', '"https://vielen-games-api.herokuapp.com"'
            ext.betaDistributionGroupAliases = "production"
        }
        staging {
            flavorDimension 'backend'
            applicationId "com.vielengames.staging"
            buildConfigField 'String', 'ENDPOINT', '"https://vielen-games-api-staging.herokuapp.com"'
            ext.betaDistributionGroupAliases = "staging"
        }
        mock {
            flavorDimension 'backend'
            applicationId "com.vielengames.mock"
            buildConfigField 'String', 'ENDPOINT', '"mock://mock"'
            ext.betaDistributionGroupAliases = "mock"
        }
        apiary {
            flavorDimension 'backend'
            applicationId "com.vielengames.apiary"
            buildConfigField 'String', 'ENDPOINT', '"https://private-0f3a-vielengamesapi.apiary-mock.com"'
            ext.betaDistributionGroupAliases = "apiary"
        }
    }
    sourceSets {
        mock {
            java { srcDir 'src/stub_login/java' }
            res { srcDir 'src/stub_login/res' }
        }
        apiary {
            java { srcDir 'src/stub_login/java' }
            res { srcDir 'src/stub_login/res' }
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile 'com.facebook.android:facebook-android-sdk:3.22.0'
    compile 'com.crashlytics.android:crashlytics:1.1.13'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    provided 'pl.mg6.hrisey:hrisey:0.3.1'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.0') {
        exclude group: 'javax.inject'
    }
}
