buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.3'
    }
}

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.vielengames"
        minSdkVersion 10
        targetSdkVersion 21
        versionCode 4
        versionName "1.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    if (project.hasProperty("STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(STORE_FILE)
                storePassword STORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    flavorDimensions 'backend'
    productFlavors {
        production {
            flavorDimension 'backend'
            applicationId "com.vielengames"
            buildConfigField 'String', 'ENDPOINT', '"https://vielen-games-api.herokuapp.com"'
        }
        staging {
            flavorDimension 'backend'
            applicationId "com.vielengames.staging"
            buildConfigField 'String', 'ENDPOINT', '"https://vielen-games-api-staging.herokuapp.com"'
        }
        mock {
            flavorDimension 'backend'
            applicationId "com.vielengames.mock"
            buildConfigField 'String', 'ENDPOINT', '"mock://mock"'
        }
        apiary {
            flavorDimension 'backend'
            applicationId "com.vielengames.apiary"
            buildConfigField 'String', 'ENDPOINT', '"https://vielengamesapi.apiary-mock.com/"'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':facebook')
    compile 'com.crashlytics.android:crashlytics:1.1.13'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    provided 'pl.mg6.hrisey:hrisey:0.3.1'
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.retrofit:retrofit:1.6.0'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.squareup.okhttp:okhttp:2.0.0-RC2'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0-RC2'
    compile 'de.greenrobot:eventbus:2.2.1'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.1'
}
